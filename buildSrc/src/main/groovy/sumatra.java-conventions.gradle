/*
 * Copyright (c) 2009 - 2021, DHBW Mannheim - TIGERs Mannheim
 */

plugins {
    id 'java'
    id 'jacoco'
    id 'idea'
    id 'ca.cutterslade.analyze'
}

sourceCompatibility = JavaVersion.VERSION_11
targetCompatibility = JavaVersion.VERSION_11

repositories {
    maven {
        url = 'https://nexus.tigers-mannheim.de/content/groups/public/'
    }

    mavenCentral()

    maven {
        url = 'https://jitpack.io/'
    }
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

test {
    exclude '**/*IntegrationTest.class'
}

jacoco {
    toolVersion = "0.8.7"
}

jacocoTestReport {
    reports {
        xml.enabled = true
        csv.enabled = false
        html.enabled = false
    }

    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it,
                    exclude: ['**/proto/**/*'])
        }))
    }

    dependsOn(test)
}

dependencies {
    annotationProcessor "org.projectlombok:lombok:1.18.20"
    compileOnly "org.projectlombok:lombok:1.18.20"
    permitUnusedDeclared "org.projectlombok:lombok:1.18.20"
}