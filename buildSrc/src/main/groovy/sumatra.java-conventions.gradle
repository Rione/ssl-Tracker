/*
 * Copyright (c) 2009 - 2021, DHBW Mannheim - TIGERs Mannheim
 */

plugins {
    id 'java'
    id 'jacoco'
    id 'idea'
    id 'ca.cutterslade.analyze'
    id 'sumatra.version-conventions'
}

sourceCompatibility = JavaVersion.VERSION_11
targetCompatibility = JavaVersion.VERSION_11

repositories {
    maven {
        url = 'https://nexus.tigers-mannheim.de/content/groups/public/'
    }

    mavenCentral()

    maven {
        url = 'https://jitpack.io/'
    }
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

test {
    exclude '**/*IntegrationTest.class'
}

jacoco {
    toolVersion = "0.8.7"
}

jacocoTestReport {
    reports {
        xml.required = true
        csv.required = false
        html.required = false
    }

    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it,
                    exclude: ['**/proto/**/*'])
        }))
    }
}

dependencies {
    annotationProcessor "org.projectlombok:lombok:1.18.22"
    compileOnly "org.projectlombok:lombok:1.18.22"
    permitUnusedDeclared "org.projectlombok:lombok:1.18.22"
}

analyzeClassesDependencies {
    // Set to false until up-to-date check works again (bug in plugin)
    logDependencyInformationToFiles = false
}

analyzeTestClassesDependencies {
    // Set to false until up-to-date check works again (bug in plugin)
    logDependencyInformationToFiles = false
}