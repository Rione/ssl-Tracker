/*
 * Copyright (c) 2009 - 2021, DHBW Mannheim - TIGERs Mannheim
 */

plugins {
    id 'java-library'
    id 'sumatra.java'
    id 'sumatra.test'
    id 'sumatra.protobuf'
}

def osClassifier = {
    def os = System.getProperty("os.name").toLowerCase()
    if (os.contains("windows")) {
        return "windows_amd64"
    } else if (os.contains("linux")) {
        return "linux_amd64"
    } else if (os.contains("mac")) {
        if (System.getProperty("os.arch") == "aarch64") {
            return "darwin_arm64"
        }
        return "darwin_amd64"
    } else {
        throw new IllegalStateException("Unknown operating system: ${os}")
    }
}()

dependencies {
    implementation project(':common')
    implementation project(':common-math')
    implementation project(':moduli-geometry')
    implementation project(':moduli-gamelog')
    implementation project(':sumatra-model')

    implementation 'com.github.TIGERs-Mannheim:moduli:4.1'
    implementation 'com.github.g3force:configurable:3.0'
    implementation 'com.github.g3force:instanceables:5.2'

    implementation 'org.apache.logging.log4j:log4j-api:2.20.0'

    implementation 'commons-lang:commons-lang:2.6'
    implementation 'commons-configuration:commons-configuration:1.10'
    implementation 'commons-io:commons-io:2.13.0'

    api 'com.google.protobuf:protobuf-java:3.23.2'
    implementation 'com.sleepycat:je:18.3.12'

    if (!project.property("compactBuild").toBoolean()) {
        runtimeOnly "com.github.TIGERs-Mannheim:ssl-game-controller-maven:3.5.3.0:${osClassifier}"
    }
}
