/*
 * Copyright (c) 2009 - 2021, DHBW Mannheim - TIGERs Mannheim
 */

plugins {
    id 'java-library'
    id 'sumatra.java'
    id 'sumatra.test'
    id 'sumatra.protobuf'
}

def osClassifier = {
    def os = System.getProperty("os.name").toLowerCase()
    def arch = System.getProperty("os.arch").toLowerCase()
    if (os.contains("windows")) {
        return "windows_amd64"
    } else if (os.contains("linux")) {
        if (arch == "amd64") {
            return "linux_amd64"
        } else if (arch == "aarch64") {
            return "linux_arm64"
        }
    } else if (os.contains("mac")) {
        if (arch == "aarch64") {
            return "darwin_arm64"
        }
        return "darwin_amd64"
    }
    throw new IllegalStateException("Unknown operating system '${os}' or architecture '${arch}'")
}()

dependencies {
    implementation project(':common')
    implementation project(':common-math')
    implementation project(':moduli-geometry')
    implementation project(':moduli-gamelog')
    implementation project(':sumatra-model')

    implementation(libs.com.github.TIGERs.mannheim.moduli)
    implementation(libs.com.github.g3force.configurable)
    implementation(libs.com.github.g3force.instanceables)

    implementation(libs.org.apache.logging.log4j.log4j.api)

    implementation(libs.commons.lang)
    implementation(libs.commons.configuration)
    implementation(libs.commons.io)

    api(libs.com.google.protobuf.protobuf.java)

    if (!project.property("compactBuild").toBoolean()) {
        runtimeOnly(variantOf(libs.com.github.TIGERs.mannheim.ssl.game.controller.maven) { classifier(osClassifier) })
    }
}
