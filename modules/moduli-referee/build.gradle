/*
 * Copyright (c) 2009 - 2021, DHBW Mannheim - TIGERs Mannheim
 */

plugins {
    id 'sumatra.java-conventions'
    id 'java-library'
    id 'sumatra.protobuf-conventions'
}

def osClassifier = {
    def os = System.getProperty("os.name").toLowerCase()
    if (os.contains("windows")) {
        return "windows_amd64"
    } else if (os.contains("linux")) {
        return "linux_amd64"
    } else if (os.contains("mac")) {
        return "darwin_amd64"
    } else {
        throw new IllegalStateException("Unknown operating system: ${os}")
    }
}()

dependencies {
    implementation project(':common')
    implementation project(':common-math')
    implementation project(':moduli-geometry')

    implementation 'com.github.TIGERs-Mannheim:moduli:4.1'
    implementation 'com.github.g3force:configurable:3.0'
    implementation 'com.github.g3force:instanceables:5.2'

    implementation 'org.apache.logging.log4j:log4j-api:2.14.1'

    implementation 'commons-lang:commons-lang:2.6'
    implementation 'commons-configuration:commons-configuration:1.10'
    implementation 'commons-io:commons-io:2.8.0'

    api 'com.google.protobuf:protobuf-java:3.17.0'
    implementation 'com.sleepycat:je:18.3.12'

    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.assertj:assertj-core:3.19.0'

    runtimeOnly "com.github.TIGERs-Mannheim:ssl-game-controller-maven:2.10.5.0:${osClassifier}"
}
