/*
 * Copyright (c) 2009 - 2021, DHBW Mannheim - TIGERs Mannheim
 */

plugins {
    id 'com.palantir.git-version' version '3.0.0'
    id "com.github.breadmoirai.github-release" version "2.4.1"
    id 'idea'

    id 'sumatra.java'
    id 'sumatra.jib'
    id 'sumatra.application'
}

ext.versionDetails = versionDetails(prefix: "version/")
version gitVersion(prefix: "version/")

dependencies {
    implementation project(':autoreferee-gui')

    implementation project(':common-gui')
    implementation project(':common-gui-config')

    implementation project(':sumatra-model')
    implementation project(':sumatra-gui-log')
    implementation project(':sumatra-gui-referee')
    implementation project(':sumatra-gui-replay')
    implementation project(':sumatra-gui-visualizer')

    implementation project(':moduli-record')
    implementation project(':moduli-autoreferee')
    implementation project(':moduli-cam')
    implementation project(':moduli-referee')
    runtimeOnly project(':moduli-wp')

    implementation 'com.github.TIGERs-Mannheim:moduli:4.1'
    implementation 'com.github.TIGERs-Mannheim:infonode:1.7.0'

    implementation 'org.apache.logging.log4j:log4j-api:2.20.0'
    runtimeOnly 'org.apache.logging.log4j:log4j-core:2.20.0'
    runtimeOnly 'org.apache.logging.log4j:log4j-1.2-api:2.20.0'
    runtimeOnly 'org.slf4j:slf4j-log4j12:2.0.7'

    implementation 'commons-cli:commons-cli:1.5.0'
}

application {
    mainClass.set('edu.tigers.autoref.AutoReferee')
}

jib {
    to {
        image = "registry.hub.docker.com/tigersmannheim/auto-referee:latest"
        auth {
            username = System.getenv("DOCKER_HUB_USERNAME") ?: ""
            password = System.getenv("DOCKER_HUB_PASSWORD") ?: ""
        }
    }
    extraDirectories {
        paths {
            path {
                from = file('config')
                into = '/config'
            }
            path {
                from = file('src/main/jib')
                into = '/'
            }
        }
    }
}

distributions {
    main {
        contents {
            from('config') {
                into 'config'
            }
        }
    }
}

githubRelease {
    token System.getenv("GITHUB_TOKEN") ?: ""
    owner "TIGERs-Mannheim"
    targetCommitish.set("master")
    repo.set("AutoReferee")
    releaseAssets distZip
    tagName { "version/${versionDetails.lastTag}" }
}
